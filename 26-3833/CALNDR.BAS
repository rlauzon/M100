5 CLEAR4000,MAXRAM:J8=50:CLS:PRINT@0," Executive Calendar - Version 01.00.00 ";:PRINT@95,"Calendars":PRINT@165,"Copyright 1983 by Tandy Corp.":PRINT@250,"All rights reserved":KEYOFF:FORX=1TO8:KEYX,CHR$(X+247):NEXT
10 DIMME$(J8),MN$(12),DY$(6),HR(6,48),HR$(48),ID$(15,2):FORX=1TO12:READMN$(X):NEXT:FORX=0TO6:READDY$(X):NEXT:ONERRORGOTO28
15 OPEN"NOTE.DO"FORINPUTAS1
26 LINEINPUT#1,N$:ME=ME+1:ME$(ME)=N$:GOTO26
28 CLOSE1:IFERR=52THENGOSUB200ELSERESUME30
30 ONERRORGOTO0
40 CLS:PRINT@131,"Preparing Calendar";
50 DIM DA$(32):FOR I=1 TO 31:DA$(I)="00":Q$=MID$(STR$(I),2):MID$(DA$(I),3-LEN(Q$),4-LEN(Q$))=Q$:NEXT I
55 FORX=0TO23:Y=X*2:HR$(Y)=DA$(X)+":00":HR$(Y+1)=DA$(X)+":30":NEXT
56 HR$(1)="00:30":HR$(48)="24:00"
60 YH(1,1)=1:YH(1,2)=5:YH(2,1)=7:YH(2,2)=5:YH(3,1)=13:YH(3,2)=5:YH(4,1)=19:YH(4,2)=21
70 LP$="õ"+STRING$(10,32)+"õ   õ   õ   õ   õ   õ   õ   õ":LB$=LP$+LP$+LP$+LP$+LP$+LP$
99 GOTO2000
120 LINE(0,0)-(0,63):LINE(0,63)-(239,63):LINE(239,63)-(239,0):LINE(0,55)-(239,55):RETURN
150 CLS:PRINT@0,"            Sun Mon Tue Wed Thu Fri Sat";:LINE(0,7)-(239,7):RETURN
160 PRINT@280,"Prev Next Any  Week  Day WRAM WTpe Menu";:LINE(0,55)-(239,55):RETURN
165 FORS=1TO6:R=(S*40)+1:PRINT@R,"          õ   õ   õ   õ   õ   õ   õ   ";:NEXT:RETURN
170 GOSUB165:PRINT@240," ";:PRINT@279," ";:PRINT@280,"           õ   õ   õ   õ   õ   õ   õ   ";:LINE(2,0)-(2,63):LINE(236,0)-(236,63)
180 PRINT@0," Week Time  Sun Mon Tue Wed Thu Fri Sat ";:LINE(33,0)-(33,63):LINE(2,7)-(236,7)
182 IFM1=M2THENX3=0:FORX2=H6TOH7:PRINT@(X3*4)+52,STR$(X2);:X3=X3+1:NEXTX2:GOTO186
184 X3=0:FORX2=H6TOH8:PRINT@(X3*4)+52,STR$(X2);:X3=X3+1:NEXTX2:FORX2=1TOH7:PRINT@(X3*4)+52,STR$(X2);:X3=X3+1:NEXTX2
186 LINE(69,15)-(236,15):RETURN
200 CLS:BEEP:BEEP:PRINT@48,"NOTE.DO not found in RAM";:PRINT@120,"Press 'ESC' to create new NOTE.DO file.":PRINT@200,"Press 'ENTER' to load NOTE.DO from tape."
210 K$=INKEY$:IFK$=""THEN210
220 IFK$=CHR$(27)THENME=0:RESUME30
230 IFK$=CHR$(13)THENCLOSE1:OPEN"CAS:NOTE.DO"FORINPUTAS1:RESUME26
240 GOTO210
310 BEEP:PRINT@280,"   Ready printer, then press 'ENTER'   ";:R$=INPUT$(1):IFR$=CHR$(27)ORR$=CHR$(13)THENRETURNELSE310
350 CLS:PRINT@0," Date Time  Dur.      Description";:LINE(0,7)-(239,7)
360 PRINT@280,"   Add       Edit    Remove    Calendar";:GOSUB120:LINE(57,55)-(57,63):LINE(117,55)-(117,63):LINE(177,55)-(177,63):RETURN
400 PRINT@161,CHR$(27);"pPreparing";:PRINT@201,"Schedule";CHR$(27);"q";
405 RETURN
500 DATA January,February,March,April,May,June,July,August,September,October,November,December
510 DATA Sunday,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday
1100 CALL 23164,0,23366:CALL 27795:MENU
2000 M=VAL(LEFT$(DATE$,2)):D6=VAL(MID$(DATE$,4,2)):Y9=VAL(RIGHT$(DATE$,2)):IFY9>80THENY9=Y9+1900ELSEY9=Y9+2000
2010 GOSUB 3000:GOSUB 150:GOSUB160:
2012 X1$=DA$(D6):PRINT@40,LB$;:P3=(P2*4)+(P1*40)+13:PRINT@P3,CHR$(27);"p";X1$;CHR$(27);"q";
2015 LINE(0,55)-(239,55):K$=MN$(M):K=INT((10-LEN(K$))/2):PRINT@41+K,K$;:PRINT@83,Y9;
2100 K$=INKEY$:IFK$=""THEN2100
2110 K=ASC(K$):IFK=255THEN1100
2120 IFK>247THEND6=H5:ONK-247GOTO4000,4100,4200,5000,6000,6950,6960
2125 IFK=108ORK=0THEN7000
2130 IFK=29ORK=28ORK=30ORK=31ORK=1ORK=6ORK=20ORK=2ORK=23ORK=26THEN2200
2140 BEEP:GOTO2100
2200 IFK=29THENP2=P2-1:H5=H5-1
2210 IFK=28THENP2=P2+1:H5=H5+1
2220 IFK=30THENP1=P1-1:H5=H5-7
2230 IFK=31THENP1=P1+1:H5=H5+7
2240 IFK=1THENH5=H5-P2:P2=0
2250 IFK=6THENH5=H5+(6-P2):P2=6
2260 IFK=20THENH5=H5-((P1-1)*7):P1=1
2270 IFK=2THENH5=H5+((H4-P1)*7):P1=H4
2280 IFK=23THENP1=1:P2=H1:H5=1
2290 IFK=26THENP1=H4:P2=H2:H5=H3
2300 IFP1<1THENP1=1:H5=H5+7
2310 IFP2>6THENP2=6:H5=H5-1
2320 IFP2<0THENP2=0:H5=H5+1
2330 IFH5>H3AND(K=31ORK=2)THENH5=H5-7:P1=P1-1
2340 IFH5>H3AND(K=6ORK=28)THENP2=H2:H5=H3:P1=H4
2350 IFH5<1THENH5=H5+7:P1=P1+1
2400 PRINT@P3,X1$;:P3=(P1*40)+13+(P2*4):X1$=STR$(H5):X1$=RIGHT$(X1$,LEN(X1$)-1):IFH5<10THENX1$="0"+X1$
2410 PRINT@P3,CHR$(27);"p";X1$;CHR$(27);"q";:GOTO2100
3000 M1=M:Y1=Y9:IFM<3THENM1=M1+12:Y1=Y1-1
3010 M1=M1+1:I=INT(365.25*Y1)+INT(30.6001*M1)+1720983:H1=I+1-7*INT((I+1)/7)
3020 IF(M=4ORM=6ORM=9ORM=11)THENH3=30
3030 IF(M=1ORM=3ORM=5ORM=7ORM=8ORM=10ORM=12)THENH3=31
3040 IFM<>2THEN3100
3050 Z2=1:Z1=VAL(RIGHT$(STR$(Y9),2)):IFZ1=0THENZ2=Y9/400-INT(Y9/400):IFZ2<>0THENH3=28:GOTO3100
3055 IFZ2=0THENH3=29:GOTO3100
3060 Z2=Z1/4-INT(Z1/4):IFZ2<>0THENH3=28ELSEH3=29
3100 P1=1:P2=H1:MID$(LB$,1,40)=LP$:MID$(LB$,161,44)=LP$:MID$(LB$,201,45)=LP$
3102 P8=H1+1:IF H1=0 THEN PS=2:P1=0:GOTO3106
3104 PS=10+(P8*4)
3106 FOR I=1 TO H3:IF (P8-1)/7=INT((P8-1)/7) THEN PS=PS+12:P1=P1+1:P8=1
3108 MID$(LB$,PS,2)=DA$(I):P8=P8+1:PS=PS+4:NEXT I
3150 H5=D6:H2=P8-2:H4=P1:P1=1:P2=H1
3151 P1=1:P2=H1:F7=0
3152 F7=F7+1:IFF7=D6THEN3160
3154 P2=P2+1:IFP2>6THENP2=0:P1=P1+1
3156 GOTO3152
3160 RETURN
4000 M=M-1:IFM<1THENM=12:Y9=Y9-1
4010 D6=1:GOSUB3000:GOTO2012
4100 M=M+1:IFM>12THENM=1:Y9=Y9+1
4110 D6=1:GOSUB3000:GOTO2012
4200 PRINT@280,"                                       ";:GOSUB120:Q1=295
4202 BEEP:PRINT@Q1,"Date : MM/YYYY";:PRINT@Q1+6," ";:Y$="":R=0
4204 R=R+1:IFR=8THEN4218
4206 S$=INPUT$(1)
4208 IF(S$=CHR$(8)ORS$=CHR$(29))ANDLEN(Y$)>1THENY$=LEFT$(Y$,LEN(Y$)-1):R=R-1:PRINT@Q1+6," ";Y$;:GOTO4206
4210 IF(S$=CHR$(8)ORS$=CHR$(29))THENY$="":R=1:PRINT@Q1+6," MM/YYYY";:PRINT@Q1+6," ";:GOTO4206
4212 IFINSTR("0123456789/",S$)=0THEN4206
4214 Y$=Y$+S$
4216 PRINT@Q1+7,Y$;:GOTO4204
4218 M=VAL(LEFT$(Y$,2)):Y=VAL(RIGHT$(Y$,4)):IFM<1ORM>12ORY<1THEN4202ELSEGOSUB160:Y9=Y:D6=1:GOSUB3000:GOTO2012
4900 REM THIS DOES THE WEEKLY SCHEDULE
5000 GOSUB400:FORX=0TO48:FORR=0TO6:HR(R,X)=0:NEXTR,X:H6=H5-P2:H7=H5+(6-P2):IFH6>0ANDH7<=H3THEN M1=M:M2=M:H8=H3:GOTO5018
5004 IFH7>H3THENH7=H7-H3:M1=M:M2=M+1:IFM2>12THENM2=1:H8=H3
5005 IFH6>0THENH8=H3:GOTO5018
5006 M2=M:M1=M-1:IFM1<1THENM1=12
5008 IFM1=4ORM1=6ORM1=9ORM1=11THENH6=30+H6:H8=30:GOTO5018
5010 IFM1<>2THENH6=31+H6:H8=31:GOTO5018
5012 R=VAL(RIGHT$(STR$(Y),2)):IFR=0THENS=Y/400-INT(Y/400):IFS<>0THENH6=28+H6:H8=28:GOTO5018
5013 IFR=0THENH6=29+H6:H8=29:GOTO5018
5014 S=R/4-INT(R/4):IFS=0THENH6=29+H6:H8=29:GOTO5018
5016 H6=28+H6:H8=28
5018 IFME=0THEN5110ELSEFORX=1TOME:
5030 S1=VAL(LEFT$(ME$(X),2)):S2=VAL(MID$(ME$(X),4,2)):IFM1=M2ANDS1=M1ANDS2>=H6ANDS2<=H7THEN5040
5032 IFS1=M1ANDM1<>M2ANDS2>=H6THEN5040
5034 IFS1=M2ANDM1<>M2ANDS2<=H7THEN5040
5036 GOTO5100
5040 S3=VAL(MID$(ME$(X),7,2)):S4=VAL(MID$(ME$(X),10,2)):S3=S3*2:IFS4>=30THENS4=1ELSES4=0
5050 IFS1=M2ANDM1<>M2THENS5=S2+(6-H7)
5051 IFS1=M1ANDM1<>M2THENS5=S2-H6
5053 IFS1=M1ANDM1=M2THENS5=S2-H6
5055 S3=S3+S4:S6=VAL(MID$(ME$(X),13,2)):S7=VAL(MID$(ME$(X),16,2)):S6=S6*2:IFS7>=30THENS7=1ELSES7=0
5060 S6=S6+S7-1:R1=S3+S6:IFR1>48THENR1=48
5070 FORR=S3TOR1:HR(S5,R)=1:NEXTR
5100 NEXTX
5110 C1=16:C2=21:GOSUB170:GOTO5400
5130 M1$=LEFT$(MN$(M1),3):PRINT@82,M1$;:PRINT@122,STR$(H6);:PRINT@162,"to";:M2$=LEFT$(MN$(M2),3):PRINT@202,M2$;:PRINT@242,STR$(H7);
5200 X3=1:FORX1=C1TOC2:PRINT@(X3*40)+46,HR$(X1);:FORY1=0TO6:X2=(X3*40)+53+(Y1*4)
5210 IFHR(Y1,X1)=1THENPRINT@X2,"*";ELSEPRINT@X2,".";
5215 IFX1=C1THENLINE(0,7)-(75,7)
5220 NEXTY1:X3=X3+1:NEXTX1:LINE(0,63)-(239,63):RETURN
5400 BEEP:GOSUB5130
5500 K$=INKEY$:IFK$=""THEN5500
5510 K=ASC(K$):IFK=255ORK=254THENGOSUB150:GOSUB160:GOTO2012
5520 IFK=108ORK=0THEN5600
5550 IF K=30 OR K=31 OR K=2 OR K=20 OR K=23 OR K=26 THEN5560ELSE5500
5560 IFK=30THENC1=C1-1:C2=C2-1
5565 IFK=31THENC1=C1+1:C2=C2+1
5570 IFK=20THENC1=C1-6:C2=C2-6
5575 IFK=2 THENC1=C1+6:C2=C2+6
5580 IFK=23THENC1=1:C2=6
5585 IFK=26THENC2=48:C1=C2-5
5590 IFC1<1THENC1=C1+1:C2=C2+1:GOTO5590
5592 IFC2>48THENC2=C2-1:C1=C1-1:GOTO5592
5594 BEEP: GOSUB5200:GOTO5500
5600 K$=INPUT$(5)
5610 GOSUB 310:IFR$<>CHR$(13)THENGOSUB170:GOTO5400
5620 GOSUB 7510:LPRINT"! Weekly Schedule!                    1 1 1 1 1 1 1 1 1 1 2 2 2 2 2 !"
5630 LPRINT"!  ";DA$(M1);"/";DA$(H6);" - ";DA$(M2);"/";DA$(H7);" ! .1.2.3.4.5.6.7.8.9.0.1.2.3.4.5.6.7.8.9.0.1.2.3.4 !"
5640 GOSUB7510:FOR X1=0 TO 6
5650 LPRINT"!    ";DY$(X1);STRING$(12-LEN(DY$(X1)),32);"! ";:FOR X2=1TO48
5660 IF HR(X1,X2)=1 THENLPRINT"*";ELSELPRINT".";
5670 NEXTX2:LPRINT" !":NEXTX1:GOSUB 7510:GOSUB 170:GOTO 5400
6000 P5=P1:P6=P2:GOSUB400:M$=DA$(M)
6010 ID$(1,1)=STRING$(39,32)
6020 D$=DA$(H5):M2$=M$+"/"+D$:K4=0:X=1:L9=0
6110 IFMID$(ME$(X),1,5)=M2$THENL9=L9+1:ID$(L9,1)=ME$(X):ID$(L9,2)=STR$(X)
6120 X=X+1:IFX>METHENGOSUB350:GOTO6200
6130 GOTO6110
6200 MX=J8-ME:IFL9>1THEN6620
6300 RR=1:FR=1:C1=1:C2=L9:IFC2>5THENC2=6
6350 R=1:FORX=C1TOC2:X3=(R*40):PRINT@X3,ID$(X,1);:R=R+1:NEXTX
6355 IFR<7THENFORX=RTO6:PRINT@(X*40),"                                       ";:NEXT
6362 LINE(0,55)-(239,55):IFL9=0THEN6400
6370 P3=40*FR:IFRR=2THENP3=P3+6
6372 IFRR=3THENP3=P3+12
6374 IFRR=4THENP3=P3+18
6380 P3$=MID$(ID$(C1+FR-1,1),YH(RR,1),YH(RR,2)):PRINT@P3,CHR$(27);"p";P3$;CHR$(27);"q";
6400 K$=INKEY$:IFK$=""THEN6400
6410 K=ASC(K$):IFK=254ORK=255THENGOSUB 8000:GOSUB150:GOSUB160:P1=P5:P2=P6:GOTO2012
6412 IFK>247THENONK-247GOTO6600,6600,6700,6700,6800,6800
6414 IFK=108ORK=0THENGOTO6900
6420 IFK=30ORK=31ORK=2ORK=20ORK=23ORK=26ORK=28ORK=29ORK=1ORK=6THENPRINT@P3,P3$:C3=C1:C4=C2:LINE(0,55)-(239,55):GOTO6430
6425 GOTO6400
6430 IFK=30THENFR=FR-1
6432 IFK=29THENRR=RR-1
6434 IFK=28THENRR=RR+1
6436 IFK=1THENRR=1
6438 IFK=6THENRR=4
6440 IFK=31THENFR=FR+1
6450 IFK=20THENFR=1
6460 IFK=2THENFR=6
6470 IF K=23 THEN C1=1:C2=L9:FR=1:RR=1:IFC2>6THENC2=6
6480 IFK=26THENC2=L9:C1=C2-5:FR=6:RR=1:IFC1<1THENC1=1
6482 IFFR<1THENC1=C1-1:C2=C2-1:FR=FR+1:GOTO6482
6484 IFFR>6ORFR>(C2-C1)+1THENC2=C2+FR-6:C1=C1+FR-6:FR=(C2-C1)+1
6486 IFRR>4THENRR=4
6488 IFRR<1THENRR=1
6490 IFC1<1THENC1=C1+1:C2=C2+1:GOTO6490
6492 IFC2>L9THENC2=C2-1:C1=C1-1:GOTO6492
6494 IFC2=C4ANDC3=C1THEN6370
6496 GOTO6350
6500 PRINT@P3,T$;:PRINT@P3-1," ";:X=0
6505 X=X+1:IFX>P2THENRETURN
6510 S$=INPUT$(1):IF(S$=CHR$(8)ORS$=CHR$(29))ANDX=1THEN6510
6520 IFS$=CHR$(8)ORS$=CHR$(29)THENX=X-1:MID$(T$,X,1)=" ":PRINT@P3+X-1,"";:GOTO6510
6525 IFS$=CHR$(27)ANDQ3=2THENX=P2+1:T$=G$:RETURN
6527 IFS$=CHR$(27)ANDQ3=1THENRETURN
6530 IFS$=CHR$(13)ANDP1=4THENRETURN
6532 B1=INSTR("0123456789:",S$):IFB1=0AND(P1=2ORP1=3)THENBEEP:GOTO6510
6534 IF(P1=2ORP1=3)ANDX=3ANDVAL(T$)=24THENT$="24:00":X=6:PRINT@P3,T$;:BEEP:RETURN
6540 IFINSTR(",;",S$)>0THENBEEP:GOTO6510
6542 IFB1=>4ANDX=1AND(P1=2ORP1=3)THENBEEP:GOTO6510
6546 IF(P1=2ORP1=3)ANDX=4ANDB1=>7THENBEEP:GOTO6510
6548 IF(P1=2ORP1=3)ANDX<>3ANDB1=11THENBEEP:GOTO6510
6549 IF(P1=2ORP1=3)ANDX=3ANDB1<>11THENBEEP:GOTO6510
6550 MID$(T$,X,1)=S$
6560 PRINT@P3,LEFT$(T$,X);:GOTO6505:RETURN
6600 IFDQ>0THEN6602ELSEIFMX<=0THENBEEP:PRINT@280,"   Memory Full, cannot add more data.  ";:FORX=1TO500:NEXT:GOSUB360:GOTO6350
6602 IFL9=14THENBEEP:PRINT@280,"  Maximum entries reached for this day ";:FORX=1TO500:NEXT:GOSUB360:GOTO6350
6605 BEEP:PRINT@40,STRING$(39,32);:ID$(L9+1,1)=STRING$(39,32):ID$(L9+1,2)="n":MID$(ID$(L9+1,1),1,5)=M2$:PRINT@40,M2$;
6610 Q3=1:P3=46:T$="..:..":P1=2:P2=5:GOSUB6500:IFS$=CHR$(27)THEN6350ELSEMID$(ID$(L9+1,1),7,5)=LEFT$(T$,X-1):P3=52:T$="..:..":P1=3:GOSUB6500
6612 IFS$=CHR$(27)THEN6350ELSEMID$(ID$(L9+1,1),13,5)=LEFT$(T$,X-1):P3=58:P1=4:T$=STRING$(21,"."):P2=21:GOSUB6500
6614 IFS$=CHR$(27)THEN6350ELSEMID$(ID$(L9+1,1),19)=LEFT$(T$,X-1)
6615 L9=L9+1:MX=MX-1:IFL9=1THEN6300
6616 N$=STRING$(39,32):IFDQ>0THENMID$(ME$(DL(DQ)),1)=ID$(L9,1):ID$(L9,2)=STR$(DL(DQ)):DQ=DQ-1
6620 X1=0:FORX2=1TOL9-1:IFID$(X2,1)>ID$(X2+1,1)THENMID$(N$,1)=STRING$(39,32):X1=X2:MID$(N$,1)=ID$(X2,1):MID$(ID$(X2,1),1)=ID$(X2+1,1):MID$(ID$(X2+1,1),1)=N$:G$=ID$(X2,2):ID$(X2,2)=ID$(X2+1,2):ID$(X2+1,2)=G$
6650 NEXT X2:IFX1>0THEN6620ELSE6300
6700 Q3=2:IFRR=1THENBEEP:GOTO6350
6710 IFRR=2THENT$="..:..":P2=5:P3=(40*FR)+6:P1=RR
6720 IFRR=3THENT$="..:..":P2=5:P3=(40*FR)+12:P1=RR
6730 IFRR=4THENT$=STRING$(21,"."):P2=21:P3=(40*FR)+18:P1=RR
6740 X2=C1+FR-1:G$=MID$(ID$(X2,1),YH(RR,1),YH(RR,2)):GOSUB6500:MID$(ID$(X2,1),YH(RR,1),YH(RR,2))=STRING$(YH(RR,2),32)
6750 MID$(ID$(X2,1),YH(RR,1),YH(RR,2))=LEFT$(T$,X-1):PRINT@P3,SPACE$(LEN(T$)):PRINT@P3,MID$(ID$(X2,1),YH(RR,1),YH(RR,2)):IFRR<>2THENBEEP:GOTO6350
6760 IF L9=1 THEN 6300ELSE6620
6800 IFL9=0THEN6400ELSEP4=(40*FR):X2=C1+FR-1:PRINT@P4,CHR$(27);"p";ID$(X2,1);CHR$(27);"q";
6810 BEEP:PRINT@280,"     Remove this Entry ?               ";:PRINT@305,"";:R$=INPUT$(1):PRINTR$;:IFR$<>"y"ANDR$<>"Y"ANDR$<>"n"ANDR$<>"N"THEN6810
6820 GOSUB360:IFR$="n"ORR$="N"THENGOTO6350
6830 IFID$(X2,2)="n"THENMX=MX+1:GOTO6840
6835 DQ=DQ+1:DG=VAL(ID$(X2,2)):MID$(ME$(DG),1)=STRING$(39,32):DL(DQ)=DG:IF DQ=10THENGOSUB 6860
6840 L9=L9-1:MID$(ID$(X2,1),1)=STRING$(39,32):IFL9=0ORX2>L9THEN6300
6842 FORX1=X2TOL9:MID$(ID$(X1,1),1)=ID$(X1+1,1):ID$(X1,2)=ID$(X1+1,2):NEXT X1
6850 GOTO6300
6860 X=1:DQ=0
6865 IFME$(X)<>STRING$(39,32)THEN6885 
6870 IFME$(ME)=STRING$(39,32)THENME=ME-1:GOTO6870
6875 IFME<XTHENMX=J8-ME:RETURN
6880 MID$(ME$(X),1)=ME$(ME):ME=ME-1
6885 X=X+1:IFX<=METHEN6865ELSEMX=J8-ME:RETURN
6900 K$=INPUT$(5):IFL9=0THENPRINT@280,"        No Data Found to print.        ";:BEEP:FORX=1TO300:NEXT:GOSUB360:GOTO6300
6905 GOSUB310:IFR$=CHR$(27)THENGOSUB360:GOTO6350
6910 LPRINTTAB(8);"Daily Schedule for ";M2$:GOSUB7500:LPRINT" Time   Dura.        Description     ":GOSUB7500
6915 FORX=1TOL9:LPRINTTAB(1);MID$(ID$(X,1),7,5);"  ";MID$(ID$(X,1),13,5);"   ";MID$(ID$(X,1),19):NEXTX
6920 GOSUB7500:GOSUB350:GOTO6350
6950 BEEP:IFME=0THENPRINT@280,"    There is no data to save to RAM.   ";:FORX=1TO400:NEXT:GOTO6980
6955 PRINT@280,"        Saving NOTE.DO to RAM          ";:GOSUB6860:OPEN"NOTE.DO"FOROUTPUTAS1:GOTO6970
6960 BEEP:IFME=0THENPRINT@280,"  There is no data to save to cassette ";:FORX=1TO400:NEXT:GOTO6980
6965 GOSUB 6860:PRINT@280,"Ready Data Cassette, then press 'ENTER'";:R$=INPUT$(1):IFR$=CHR$(13)THENPRINT@280,"         Writing data to tape.         ";:OPEN"CAS:NOTE.DO"FOROUTPUTAS1:GOTO6970
6967 IFR$=CHR$(27)THEN6980ELSE6965
6970 FORX=1TOME:PRINT#1,ME$(X):NEXTX:CLOSE1:BEEP
6980 GOSUB160:GOTO2100
7000 K$=INPUT$(5):GOSUB310:IFR$=CHR$(27)THENGOSUB160:D6=H5:GOTO2012
7010 GOSUB7500:L$=MN$(M)+STR$(Y9):L=(40-LEN(L$))/2:LPRINT TAB(L);L$
7020 GOSUB7500
7030 LPRINT"   Sun  Mon  Tue  Wed  Thu  Fri  Sat    ":GOSUB7500
7040 P2=H1:FORX1=1TOH3
7100 X2=(P2*5)+3:LPRINT TAB(X2);X1;:P2=P2+1:IFP2>6THENP2=0:LPRINT:GOSUB7500
7110 NEXTX1:IFP2<>0THENLPRINT:GOSUB7500
7120 GOTO2000
7500 LPRINT STRING$(40,"-"):RETURN
7510 LPRINT STRING$(69,"-"):RETURN
8000 FOR X1=1 TO L9
8020 DG=VAL(ID$(X1,2)):IF DG=0 THEN 8040ELSE MID$(ME$(DG),1)=ID$(X1,1):GOTO 8100
8040 IF DQ=0 THEN ME=ME+1:ME$(ME)=ID$(X1,1):GOTO 8100
8050 MID$(ME$(DL(DQ)),1)=ID$(X1,1):DQ=DQ-1
8100 NEXT X1:L9=0:MID$(ID$(1,1),1)=STRING$(39,32):RETURN
