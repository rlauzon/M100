10 'PRINT.BA: Document Printer
20 'Copyright 1983 by Tandy Corp. All rights reserved.
30 MAXFILES=2: CLEAR 500: DEFINT A-Z
40 DIM TF$(1),PM$(11),PM(11)
50 PW=1:PL=2:LM=3:RM=4:TM=5:BM=6:LS=7:NF=8:TF=9:PZ=10:FF=11
60 VW=40:VH$=CHR$(27)+"p":VN$=CHR$(27)+"q":EF$=CHR$(27)+"J"
70 TF$(0)="NO": TF$(1)="YES": PN$="Page"
80 FOR I=1 TO 11: READ PM$(I):NEXT I
90 DATA Page width,Page length,Left margin,Right mgn.,Top margin,Bottom mgn.,Line space,Number pgs,Title pgs,Pg-pause,Skip headings 1st pg
100 FOR I=1 TO 11: READ PM(I):NEXT I
110 DATA 80,66,10,10,6,6,1,1,1,0,0
120 FF$=CHR$(12):CR$=CHR$(13):LF$=CHR$(10):TB$=CHR$(9):BS$=CHR$(8):SP$=" ":HY$="-": CC$=FF$+CR$+LF$+TB$+BS$+SP$
130 CLS
140 FILES
150 INPUT"Print which file"; FI$
160 INPUT"Output device (press ENTER for printer)"; FO$
170 IF FO$="" THEN FO$="LPT:"
180 CLS: FOR I=1 TO 7: PRINT @ VW/2*(I-1),VH$;I;VN$;PM$(I)" ="PM(I);:NEXT I
190 FOR I=8 TO 11: PRINT @VW/2*(I-1),VH$;I;VN$;PM$(I)" = "TF$(PM(I));:NEXT I
200 S=0:PRINT:INPUT"Change which setting (0=no change)";S
210 IF S=0 THEN 250
220 IF S>=1 AND S<=7 THEN PRINT"Enter a new value for '"PM$(S)"'";:INPUT PM(S):GOTO 180
230 IF S>=8 AND S<=11 THEN PRINT "'"PM$(S)"' (YES/NO)";:INPUT S1$: IF S1$="YES"OR S1$="yes" THEN PM(S)=1 ELSE IF S1$="NO" OR S1$="no" THEN PM(S)=0
240 GOTO 180
250 LW=PM(PW)-PM(LM)-PM(RM): TL=PM(PL)-PM(BM):LM$=STRING$(PM(LM),SP$)
260 CLS: IF PM(TF)=1 THEN PRINT "Enter a one-line title maximum"LW"characters.":LINE INPUT TI$: TI$=LEFT$(TI$,LW)
270 IF FO$="LPT:" OR FO$="lpt:" THEN INPUT "Align paper then press ENTER";RD$
280 OPEN FI$ FOR INPUT AS 1
290 OPEN FO$ FOR OUTPUT AS 2
300 CLS
310 C$="": PN=0
320 IF EOF(1) THEN CLOSE: PRINT "Done": GOTO 140
330 IF PM(PZ)=1 THEN INPUT"Press ENTER to print next page"; RD$
340 PN=PN+1: LN=0
350 IF LN<PM(TM) THEN GOSUB 660: GOTO 350
360 IF PN=1 AND PM(FF)=1 THEN 400
370 IF PM(TF)=1 THEN BH$=TI$: GOSUB 670
380 IF PM(NF)=1 THEN BH$=PN$+STR$(PN): GOSUB 670
390 IF PM(TF)=1 OR PM(NF)=1 THEN GOSUB 660:LK=1:GOSUB 700
400 IF EOF(1) THEN 530
410 IF LN>=TL THEN 630
420 C1$=C$: C$=INPUT$(1,1)
430 ON INSTR(1,CC$,C$) GOTO 530,550,570,590,620,650
440 B$=B$+C$: IF C$>=SP$ THEN CP=CP+1
450 IF CP<LW THEN WF=0: GOTO 400
460 WF=1: LB=LEN(B$): SP=1: HP=0: S=1
470 HQ=HP:HP=INSTR(S,B$,HY$):IF HP>0 THEN S=HP+1:GOTO 470
480 HP=HQ:S=1:SP=0
490 SQ=SP: SP=INSTR(S,B$,SP$):IF SP>0 THEN S=SP+1:GOTO 490
500 SP=SQ:BP=-(SP>HP)*SP-(HP>=SP)*HP
510 IF BP=0 THEN GOSUB 680: GOTO 400
520 B1$=RIGHT$(B$,LB-BP):B$=LEFT$(B$,BP): GOSUB 680:B$=B1$:CP=LEN(B$):GOTO 400
530 AF=0:WF=0:IF B$>""THEN GOSUB 680
540 GOTO 630
550 IF WF=0 THEN GOSUB 680ELSE WF=0
560 GOTO 400
570 IF C1$=CR$ OR AF=1 OR WF=1 THEN 400
580 GOSUB 680: GOTO 400
590 I1=8-CP MOD 8: I2=LW-CP
600 TB=-(I1<I2)*I1-(I2<=I1)*I2
610 CP=CP+TB:B$=B$+STRING$(TB,SP$):GOTO 450
620 B$=B$+C$:CP=CP-1:WF=0:GOTO 400
630 IF LN<PM(PL) THEN GOSUB 660: GOTO 630
640 GOTO 320
650 IF CP=0 THEN 400ELSE 440
660 PRINT #2,"":LN=LN+1:RETURN
670 PRINT #2,LM$BH$: LN=LN+1:RETURN
680 IF B$>"" THEN PRINT#2,LM$;
690 PRINT#2,B$:LN=LN+1: B$="":CP=0:LK=1
700 IF LK<PM(LS) AND LN<TL THEN GOSUB 660: LK=LK+1: GOTO 700
710 RETURN
