10 'SORT.BA: Sorting Program
20 'Copyright 1983 by Tandy Corp. All rights reserved.
30 MAXFILES=1:CLEAR 0:CLEAR FRE(0)-1000:DEFINT A-Z:CM$=",":QT$=CHR$(34):VW=40
40 FILES:LINEINPUT"Name of input file? ";FI$:LINEINPUT"Name of output file? ";FO$:IF FI$=""THEN END ELSE IF FO$=""THEN FO$="LCD:"
50 ONERROR GOTO 60:OPEN FI$ FOR INPUT AS 1:ONERROR GOTO 0    :GOTO 70
60 BEEP:PRINT"Can't open "FI$" due to error"ERR:RESUME 40
70 IF EOF(1) THEN BEEP:PRINT"Empty file.":GOTO 40
80 LINEINPUT#1,SL$:IF LEN(SL$)=255THEN940
90 PRINT"Here is a sample record:"
100 PRINTSL$
110 NL=1
120 IF NOT EOF(1) THEN LINEINPUT#1,D$:IF LEN(D$)=255 THEN 940ELSE NL=NL+1:GOTO 120
130 CLOSE 1:IF NL=1 THEN BEEP:PRINT "Aborted.":PRINT"Input file contains only 1 record.":END
140 DIM DB$(NL),SQ(NL):FOR I=1 TO NL:SQ(I)=I:NEXT I
150 PRINT"Fixed- or variable-length fields (F/V)?";:F$=INPUT$(1):PRINT
160 FT=INSTR(1,"FfVv",F$)/2+.5:ON FT GOTO 360,180
170 GOTO 150
180 PRINT"Analyzing sample record":NF=1:QF=0:I=0
190 I=I+1:IF I>LEN(SL$) THEN260
200 C$=MID$(SL$,I,1):ON INSTR(1,CM$+QT$,C$) GOTO 220,240
210 GOTO 190
220 IF QF=0 THEN NF=NF+1
230 GOTO 190
240 QF=QF XOR 1
250 GOTO 190
260 PRINT"Record contains"NF"fields."
270 IF NF=1 THEN SF=1:GOTO 530
280 PRINT"Fields are"
290 OPEN FI$ FOR INPUT AS 1
300 FOR I=1 TO NF:INPUT#1,F$:PRINT I"- "QT$F$QT$:NEXT I:CLOSE
310 PRINT"Agreed (Y/N)?";:YN$=INPUT$(1):PRINT:ON INSTR(1,"YyNn",YN$)/2+.5 GOTO 340,330
320 GOTO 310
330 PRINT"Can't sort file. Check record format.":END
340 PRINT"Sort on which field (0 to review)";:INPUT SF:IF SF<1 OR SF>NF THEN 280
350 GOTO 530
360 CLS:PRINT"Find starting and ending column":PRINT"of sort field:"
370 NS=-INT(-LEN(SL$)/VW):LS=LEN(SL$)-(NS-1)*VW
380 S=1:LC=VW
390 IF S=NS THEN LC=LS
400 FOR I=1 TO LC:TC=I+(S-1)*VW
410 IF I MOD 10=0 THEN PRINTCHR$(TC/10+48);:GOTO 440
420 IF I MOD 5=0 THEN PRINT".";:GOTO 440
430 PRINT"-";
440 NEXT I
450 IF S=NS AND SL<VW THEN PRINT
460 FOR I=1 TO LC:TC=I+(S-1)*VW:PRINTMID$(SL$,TC,1);:NEXT I
470 IF S<NS THEN S=S+1:PRINT:INPUT"Press ENTER to view next line";DY$:CLS:PRINT:GOTO 390
480 IF LS<VW THEN PRINT
490 PRINT:PRINT"Enter starting and ending columns of":PRINT"sort field (0,0 to review sample data)":INPUT SC,EC
500 IF SC<1 OR SC>EC OR EC>LEN(SL$) THEN360
510 PRINT"Here is the field you selected:":PRINTMID$(SL$,SC,EC-SC+1)
520 PRINT"Ok (Y/N)?";:YN$=INPUT$(1):PRINT:IF YN$<>"Y"AND YN$<>"y"THEN 360
530 PRINT"What type of data is in that field":PRINT"<A>lpha   <N>umeric   <D>ate mm/dd/yy":PRINT"Select one: A/N/D";
540 DY$=INPUT$(1):PRINT:DT=INSTR(1,"AaNnDd",DY$)/2+.5:IF DT=0 THEN BEEP:GOTO 530
550 IF DT=2 THEN DIM DB!(NL)
560 IF FT=2 THEN630
570 OPEN FI$ FOR INPUT AS 1
580 FOR L=1 TO NL
590 IF EOF(1) THEN DY$="" ELSE LINEINPUT #1,DY$:DY$=MID$(DY$,SC,EC-SC+1)
600 ON DT GOSUB 820,830,840
610 NEXT L:CLOSE
620 GOTO 680
630 OPEN FI$ FOR INPUT AS 1
640 FOR L=1 TO NL:FOR F=1 TO NF
650 IF EOF(1) THEN DY$="" ELSE INPUT #1,DY$
660 IF F=SF THEN ON DT GOSUB 820,830,840
670 NEXT F,L:CLOSE
680 PRINT "Sorting the fields now"
690 SW=0:FOR I=1 TO NL-1
700 IF DT=2 THEN 720
710 IF DB$(SQ(I))>DB$(SQ(I+1))THEN730ELSE740
720 IF DB!(SQ(I))<=DB!(SQ(I+1))THEN740
730 Q=SQ(I):SQ(I)=SQ(I+1):SQ(I+1)=Q:SW=1
740 NEXT I:IF SW>0 THEN 690
750 PRINT"Sort completed. Writing data to":PRINT"the output file."
760 OPEN FI$ FOR INPUT AS 1
770 FOR I=1 TO NL:LINEINPUT#1,DB$(I):NEXT I
780 CLOSE:ONERROR GOTO 790:OPEN FO$ FOR OUTPUT AS 1:GOTO 800
790 BEEP:PRINT"Can't open "FO$" due to error"ERR:ON ERROR GOTO 0    
800 FOR I=1 TO NL:PRINT#1, DB$(SQ(I)):NEXT I
810 CLOSE:PRINT"Sort completed.":END
820 DB$(L)=DY$:RETURN
830 DB!(L)=VAL(DY$):RETURN
840 T1$="000000"
850 T1=INSTR(1,DY$,"/"):IF T1<2 OR T1>3THEN 920
860 T2=INSTR(T1+1,DY$,"/"):IF T2<4 OR T2>6 THEN 920
870 TM=VAL(DY$):TD=VAL(MID$(DY$,T1+1,T2-T1)):TY=VAL(RIGHT$(DY$,LEN(DY$)-T2))
880 IF TM<1 OR TM>12 OR TD<1 OR TD>31 OR TY<0 OR TY>99 THEN 920
890 T5$=STR$(TY):GOSUB 930:T1$=T5$
900 T5$=STR$(TM):GOSUB 930:T1$=T1$+T5$
910 T5$=STR$(TD):GOSUB 930:T1$=T1$+T5$
920 DB$(L)=T1$:RETURN
930 T5$=RIGHT$("0"+RIGHT$(T5$,LEN(T5$)-1),2):RETURN
940 BEEP:PRINT"Aborted. One of the records in "FI$:PRINT"exceeds 254 characters in length":END
