10 'DB.BA: Database Program
20 'Copyright 1983 by Tandy Corp. All rights reserved.
30 PRINT"<U>pdate or <A>dd records?": R$=INPUT$(1):ON INSTR(1,"AaUu",R$)/2+.5 GOTO 50,60
40 GOTO 30
50 MAXFILES=1: GOTO 70
60 MAXFILES=2
70 CLEAR 1000:DEFSNG A-C,E-Z:DEFDBL D
80 ON ERROR GOTO 1390
90 VL=8:VW=40
100 VU=VL-2: LZ=VU*VW:
110 BK$=CHR$(8): CR$=CHR$(13): ES$=CHR$(27): LF$=CHR$(29): RT$=CHR$(28): UP$=CHR$(30): DN$=CHR$(31): SP$=CHR$(32): DE$=CHR$(127):QT$=CHR$(34)
120 TB$=BK$+CR$+LF$+RT$+UP$+DN$+ES$
130 LB$=ES$+"Y  <D>ata entry"+ES$+"Y! <F>ile"+ES$+"Y"+CHR$(34)+" <G>et a record from the input file"+ES$+"Y# <S>earch input file"+ES$+"Y$ <E>rase current record"+ES$+"Y% <Q>uit"
140 CS$=ES$+"P"
150 SK$=ES$+"W":SL$=ES$+"V"
160 MT$="DdFfGgSsEeQq":YN$="YyNn"
170 DI$=STRING$(VW,SP$)
180 VH$=ES$+"p": VN$=ES$+"q"
190 KY$=VH$+"ENTER"+VN$+"=data   "+VH$+CHR$(155)+VN$+SP$+VH$+CHR$(154)+VN$+SP$+VH$+CHR$(153)+VN$+SP$+VH$+CHR$(152)+VN$+"=move   "+VH$+"ESC"+VN$+"=menu"
200 EL$=ES$+"K"
210 FILES:LINE INPUT "Name of format file? "; FF$
220 OPEN FF$ FOR INPUT AS 1
230 INPUT #1, NL
240 DIM L$(NL)
250 FOR J=1 TO NL: LINE INPUT #1, L$(J): NEXT J
260 INPUT #1, NF
270 DIM LL(NF),LC(NF),T$(NF),PC$(NF),D(NF),D$(NF),DV(NF)
280 FOR F=1 TO NF
290 INPUT #1, LL(F),LC(F),DS$
300 T$(F)=LEFT$(DS$,1)
310 IF T$(F)="A" THEN 320ELSE 350
320 LF=VAL(RIGHT$(DS$,LEN(DS$)-1))
330 IF LF=1 THEN PC$(F)="!": GOTO 360
340 IF LF>1 AND LF<=VW THEN PC$(F)="\"+STRING$(LF-2,32)+"\": GOTO 360: ELSE BEEP:PRINT"Field exceeds display width":GOTO 970
350 PC$(F)=RIGHT$(DS$,LEN(DS$)-1)
360 NEXT F
370 CLOSE #1:ON ERROR GOTO 1400
380 IF MAXFILES=2 THEN LINE INPUT"Name of input file? ";GF$: OPEN GF$ FOR INPUT AS 2
390 LINE INPUT "Name of output file? ";OF$:IF INSTR(1,OF$,"CAS:")>0 THEN 420
400 PRINT"<D>estroy old output file (if any) or":PRINT"<A>ppend?":DA$=INPUT$(1):PRINT:ON INSTR(1,"DdAa",DA$)/2+.5 GOTO 420,430
410 GOTO 400
420 OPEN OF$ FOR OUTPUT AS 1:GOTO 440
430 OPEN OF$ FOR APPEND AS 1
440 SCREEN 0,0:FOR I=1 TO 8: KEY I,"": NEXT I
450 PRINTSL$;
460 PG=(NL-1)\VU+1: PL=(NL-1)MOD VU+1
470 GOSUB 1370
480 FC=1
490 GOSUB 1360
500 CLS:PRINT CS$;
510 IF PC=PG THEN PS=PL ELSE PS=VU
520 FOR I=1 TO PS: PRINTL$((PC-1)*VU+I): NEXT I
530 TT=(PC-1)*VU+1: TB=TT+PS-1
540 FOR I=1 TO NF
550 IF LL(I)>=TT AND LL(I)<=TB THEN FD=I: GOSUB 1280
560 NEXT I
570 GOSUB 1340: FD=FC: GOSUB 1280: GOSUB 1350
580 SCREEN 0,0:PRINT@LZ+VW+2,KY$;:PRINT@LZ,EL$;
590 DP=0
600 SOUND 2400,1
610 K$=INKEY$: IF K$=""OR K$=QT$THEN610
620 IF K$>=SP$ AND K$<DE$ THEN 650
630 ON INSTR(1,TB$,K$) GOTO 660,670,700,720,760,780,850
640 GOTO 610
650 IF DP=VW THEN 600ELSE PRINTK$;: DP=DP+1: MID$(DI$,DP)=K$: GOTO 610
660 IF DP=0 THEN 600ELSE PRINT DE$;: DP=DP-1: GOTO 610
670 DV(FC)=1: IF T$(FC)="N" THEN 690
680 D$(FC)=LEFT$(DI$,DP): GOTO 720
690 D(FC)=VAL(LEFT$(DI$,DP)): GOTO 720
700 FO=FC: FC=FC-1: IF FC<1 THEN FC=1: GOTO 600
710 GOTO 730
720 FO=FC:FC=FC+1: IF FC>NF THEN FC=NF:GOTO 570
730 PO=PC: GOSUB 1360
740 IF PC<>PO THEN 500
750 FD=FO: GOSUB 1280: GOTO 570
760 PO=PC: PC=PC-1: IF PC<1 THEN PC=1: GOTO 600
770 GOTO 790
780 PO=PC: PC=PC+1: IF PC>PG THEN PC=PG: GOTO 600
790 IF PC=PG THEN PS=PC+PL ELSE PS=VU
800 TT=(PC-1)*VU+1: TB=TT+PS-1: FF=0
810 FOR I=1 TO NF
820 IF LL(I)>=TT AND LL(I)<=TB THEN FF=I: I=NF
830 NEXT I
840 IF FF=0 THEN STOP ELSE FC=FF: GOTO 500
850 CLS:PRINTLB$
860 PRINTEL$;
870 R$=INPUT$(1):ON INSTR(1,MT$,R$)/2+.5 GOTO 500,890,980,1040,960,970
880 GOTO 870
890 FOR I=1 TO NF
900 IF T$(I)="N" THEN 920
910 PRINT#1,QT$D$(I)QT$;: GOTO 930
920 PRINT #1,D(I);
930 IF I<NF THENPRINT#1,",";
940 NEXT I:PRINT#1,
950 CLS:PRINT@LZ,"Record filed.":GOSUB 1380:GOSUB 1370:GOTO 850
960 GOSUB 1370:GOTO 480
970 CLOSE: PRINTSK$;: CALL 23164,0,23366: CALL 27795: END
980 IF MAXFILES=1 THEN 1270
990 EF=0:FOR I=1 TO NF:IF EOF(2)THEN EF=1:CLS:PRINT@LZ,"End of file."EL$: I=NF:GOSUB 1380:GOTO 1030
1000 IF T$(I)="N" THEN 1010ELSE INPUT #2,D$(I):GOTO 1020
1010 INPUT#2,DY$:D(I)=VAL(DY$)
1020 DV(I)=1
1030 NEXT I: IF EF=1 THEN 850ELSE 480
1040 IF MAXFILES=1 THEN 1270
1050 PRINT@LZ+VW,"Search hi-lited field only. OK? (Y/N)"EL$;:R$=INPUT$(1)
1060 ON INSTR(1,YN$,R$)/2+.5 GOTO 1080,500
1070 GOTO 1050
1080 MF=0:EF=0:PRINT@LZ+VW,"Enter the search value: "EL$;
1090 IF T$(FC)="N" THEN 1100ELSE INPUT SV$:GOTO 1110
1100 INPUT SV
1110 PRINT@LZ+VW,"Start at 1st or next record? (F/N)"EL$;:R$=INPUT$(1)
1120 ON INSTR(1,"FfNn",R$)/2+.5GOTO 1140,1150
1130 GOTO 1110
1140 CLOSE #2:OPEN GF$ FOR INPUT AS 2
1150 PRINT@LZ+VW,"Searching"EL$;:FOR I=1 TO NF:IF EOF(2) THEN EF=1: CLS:PRINT@LZ,"Stopped at end of file."EL$:I=NF:GOSUB 1380:GOSUB 1370:GOTO 1240
1160 D$(I)="":D(I)=0
1170 IF T$(I)="N" THEN 1180ELSE INPUT #2,D$(I):GOTO 1190
1180 INPUT #2,DY$:D(I)=VAL(DY$)
1190 DV(I)=1: IF FC<>I THEN 1240
1200 IF T$(FC)="N" THEN 1230
1210 IF LEFT$(D$(FC),LEN(SV$))=SV$ THEN MF=1
1220 GOTO 1240
1230 IF D(I)=SV THEN MF=1
1240 NEXT I
1250 IF EF=1 THEN850ELSE IF MF=1 THEN 500
1260 GOTO 1150
1270 CLS:PRINT@LZ,"Input file not requested at startup."EL$:GOSUB 1380:GOTO 850
1280 PZ=((LL(FD)-1)MOD VU)*VW+LC(FD)-1
1290 IF DV(FD)<>0 THEN IF T$(FD)="A" THEN 1310ELSE 1320
1300 PRINT@ PZ, STRING$(LEN(PC$(FD)),32);: GOTO 1330
1310 PRINT@ PZ, USING PC$(FD);D$(FD);:GOTO 1330
1320 PRINT@ PZ, USING PC$(FD);D(FD);
1330 RETURN
1340 PRINT VH$;:RETURN
1350 PRINT VN$;:RETURN
1360 PC=(LL(FC)-1)\VU+1: RETURN
1370 FOR I=1 TO NF: D$(I)="": D(I)=0: DV(I)=0: NEXT I:RETURN
1380 BEEP:INPUT "Press ENTER to continue";EN$:RETURN
1390 BEEP:PRINT"Problem in format file."
1400 PRINT"Program stopped due to error"ERR:PRINT"at line "ERL;
1410 RESUME 970
