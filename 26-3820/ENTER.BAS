10 'ENTRY.BA: Data Entry Program
20 'Copyright 1983 by Tandy Corp. All rights reserved.
30 MAXFILES=1:CLEAR 256:DEFINT A-Z
40 ES$=CHR$(27):EF$=ES$+"J":CR$=CHR$(13):NL$=CR$+CHR$(10):VH$=ES$+"p":VN$=ES$+"q":HI=4697:LO=5272:QT$=CHR$(34):CM$=","
50 DIM N(12)
60 FOR I=1 TO 12:READ N(I):NEXT I
70 DATA 3134,2959,2793,2636,2484,2348,2216,2092,1975,1864,1758,1660
80 CLS:FILES:LINE INPUT "Name of output file? ";FO$
90 PRINT"<A>ppend to or <D>estroy existing":PRINT"file (if any) ";
100 AD$=INPUT$(1):ON INSTR(1,"AaDd",AD$)/2+.5 GOTO 120,130
110 BEEP: GOTO 100
120 OPEN FO$ FOR APPEND AS 1:GOTO 140
130 OPEN FO$ FOR OUTPUT AS 1
140 PRINT:INPUT "How many items per record (1-8)";NI:IF NI<1 OR NI>8 THEN 140
150 DIM DT(NI),DP$(NI),RG#(NI,2),D$(NI)
160 FOR I=1 TO NI
170 PRINT:DP$(I)="Item"+STR$(I):PRINT"Name of item"I"(10 characters max)":INPUT DP$(I):DP$(I)=LEFT$(DP$(I),10)
180 PRINT"Data type of '"DP$(I)"'":PRINT"<A>lpha or <N>umeric ";
190 AN$=INPUT$(1):DT=INSTR(1,"AaNn",AN$)/2+.5:IF DT=0 THEN BEEP:GOTO 190ELSE DT(I)=DT
200 IF DT(I)=1 THEN 260
210 PRINT:PRINT"Enter minimum and maximum values"NL$"for '"DP$(I)"' (ENTER=no limit)."
220 MI$="-1E62":INPUT "Minimum value";MI$
230 RG#(I,1)=VAL(MI$)
240 PRINT:MA$="1E62":INPUT "Maximum value";MA$
250 RG#(I,2)=VAL(MA$)
260 NEXT I
270 CLS:FOR I=1 TO NI
280 PRINT@160,EF$"Enter data for '"DP$(I)"' "VH$"ENTER"VN$"=quit"
290 IF DT(I)=1 THEN 310
300 PRINT"Min="RG#(I,1);TAB(20)"Max="RG#(I,2)
310 SOUND 2500,1:D$="":INPUT D$:IF D$="" THEN BEEP:CLOSE:END
320 IF DT(I)=1 THEN D$(I)=D$:GOTO 390
330 D#=VAL(D$):IF D#>=RG#(I,1) AND D#<=RG#(I,2) THEN 380
340 SOUND HI,8:SOUND LO,8
350 PRINT"Out of range. <A>ccept or <C>ancel? ";
360 AC$=INPUT$(1):ON INSTR(1,"AaCc",AC$)/2+.5 GOTO 380,280
370 GOTO 360
380 D$(I)=STR$(D#)
390 FOR J=1 TO I:JC=(J-1)\4:JR=(J-1) MOD 4
400 PRINT@JR*40+JC*20,VH$DP$(J)VN$D$(J);
410 NEXT J
420 NEXT I
430 FOR N=1 TO 5:SOUND N(N),1:NEXT N
440 PRINT@160,EF$"<S>tore <R>edo "
450 SR$=INPUT$(1):ON INSTR(1,CR$+CR$+"SsRr",SR$)/2+.5 GOTO 470,470,270
460 GOTO 450
470 FOR I=1 TO NI
480 IF DT(I)=1 THEN PRINT#1,QT$;
490 PRINT#1,D$(I);
500 IF DT(I)=1 THEN PRINT#1,QT$;
510 IF I<NI THEN PRINT#1,CM$;
520 NEXT I
530 PRINT#1,
540 GOTO 270
550 CLOSE: END
