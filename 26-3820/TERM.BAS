10 'TERM.BA: Terminal Program
20 'Copyright 1983 by Tandy Corp. All rights reserved.
30 MAXFILES=3:CLEAR 256,HIMEM-14:DEFINT A-Z
40 DIM KD$(3,8),DX$(1),PX$(1),QI
50 AD!=HIMEM:FOR I=1 TO 13:READ B:POKE AD!+I,B:NEXT I:RD!=HIMEM+1
60 DATA 235,205,109,109,62,255,18,200,205,126,109,18,201
70 FOR S=1 TO 3:FOR K=1 TO 8:READ KD$(S,K):NEXT K,S
80 DATA File,Down," Up",,," ^C",Time,Bye
90 DATA File,X-Dn,Mem,,," ^C",Time,Bye
100 DATA File,,X-Up,,,,Time,Bye
110 DW$="7":PY$="I":SB$="1":PH$="555-1111<>"
120 DX$(0)="Full":DX$(1)="Half":PX$(0)="":PX$(1)="Echo":BR$=CHR$(3)
130 DX=0:PX=0
140 PR$="Word length":RG$="7/8":V$=DW$:GOSUB 180:DW$=V$
150 PR$="Parity":RG$="E/O/I/N":V$=PY$:GOSUB 180:PY$=V$
160 PR$="Stop bits":RG$="1/2":V$=SB$:GOSUB 180:SB$=V$
170 CW$="MDM:"+DW$+PY$+SB$+"E":GOTO 210
180 CLS:PRINT@80,PR$"="V$:PRINT"Press ENTER to leave as-is or":PRINT"ENTER a new value ("RG$")";
190 V1$=V$:INPUT V1$:IF INSTR(1,RG$,V1$) MOD 2=0 THEN BEEP:GOTO180
200 V$=V1$:RETURN
210 CLS:PRINT@80,"Phone number to call is "PH$:PRINT"Press ENTER to leave as-is or":PRINT"Enter a new number? ";:LINE INPUT P1$: IF P1$>""THEN PH$=P1$
220 IF INSTR(1,PH$,"<")=0 THEN PH$=PH$+"<>"
230 M!=VARPTR(PH$):CD!=PEEK(M!+1)+PEEK(M!+2)*256
240 PRINT"Calling host computer..."
250 CALL 21200:CALL 21293,0,CD!
260 OPEN CW$ FOR OUTPUT AS 1:OPEN CW$ FOR INPUT AS 2
270 BEEP: S=1: GOSUB 660
280 CALL RD!,0,VARPTR(QI):IF QI=255 THEN 320
290 CI$=CHR$(QI):PRINTCI$;
300 IF S=2 THEN PRINT#3,CI$;
310 IF PX=1 THEN ON ERROR GOTO 410:LPRINT CI$;:ON ERROR GOTO 0    
320 IF S=3 THEN 350
330 IF BR=1 THEN CO$=BR$:BR=0:GOTO 360
340 CO$=INKEY$:IF CO$=""THEN 280ELSE 360
350 IF EOF(3) THEN GOSUB 650:GOTO 280ELSE CO$=INPUT$(1,3)
360 PRINT#1,CO$;
370 IF DX=1 AND PX=1 THEN ON ERROR GOTO 410:LPRINT CO$;:ON ERROR GOTO 0    
380 IF DX=0 THEN 280
390 PRINTCO$;:IF S=2 THEN PRINT#3,CO$;
400 GOTO 280
410 BEEP:PRINT:PRINT"Echo function aborted.":GOSUB 580:RESUME NEXT
420 PRINT:FILES:RETURN
430 PRINT:LINE INPUT"Download file name? ";FO$
440 IF INSTR(1,FO$,"CAS:")>0 THEN A$="N" ELSE INPUT "Append or New file (A/N)";A$
450 ON ERROR GOTO 510
460 IF A$="A" OR A$="a" THEN 480
470 OPEN FO$ FOR OUTPUT AS 3:GOTO 490
480 OPEN FO$ FOR APPEND AS 3
490 ON ERROR GOTO 0    :S=2:GOSUB 660
500 RETURN
510 BEEP:PRINT"Aborted":RESUME 500
520 PRINT:LINE INPUT"Upload file name? ";FI$
530 ON ERROR GOTO 560:OPEN FI$ FOR INPUT AS 3:ON ERROR GOTO 0    
540 S=3:GOSUB 660
550 RETURN
560 BEEP:PRINT"Aborted":RESUME 550
570 DX=1 XOR DX:KEY 4,DX$(DX):SCREEN 0,1:RETURN
580 PX=1 XOR PX:KEY 5,PX$(PX):SCREEN 0,1:RETURN
590 BR=1:RETURN
600 PRINT:PRINTTIME$:RETURN
610 PRINT:PRINT"Disconnect (Y/N)?";:A$=INPUT$(1):PRINT:IF A$<>"Y"AND A$<>"y" THEN RETURN
620 GOTO 820
630 PRINT:IF INSTR(1,FO$,"CAS:")>0 THEN PRINT"Function not available for CAS: files":RETURN
640 CLOSE 3: PRINT"Free memory="FRE(0):OPEN FO$ FOR APPEND AS 3:RETURN
650 CLOSE 3: S=1:GOSUB 660:RETURN
660 SCREEN 0,0
670 FOR K=1 TO 8:KEY(K) OFF
680 IF K=4 THEN KEY K,DX$(DX):GOTO 710
690 IF K=5 THEN KEY K,PX$(PX):GOTO 710
700 KEY K,KD$(S,K)
710 NEXT K
720 ON S GOSUB 760,780,800
730 FOR K1=1 TO 8:KEY(K1) ON:NEXT K1
740 SCREEN 0,1
750 RETURN
760 ON KEY GOSUB 420,430,520,570,580,590,600,610
770 RETURN
780 ON KEY GOSUB 420,650,630,570,580,590,600,610
790 RETURN
800 ON KEY GOSUB 420,,650,570,580,,600,610
810 RETURN
820 CLOSE: CALL 23164,0,23366:CALL 27795:CLEAR 256,HIMEM+14:END
