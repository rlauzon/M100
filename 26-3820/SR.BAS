10 'SR.BA: Global Search and Replace Program
20 'Copyright 1983 by Tandy Corp. All rights reserved.
30 MAXFILES=2: CLEAR 1000
40 DEFINT A-P: DEFSTR Q-Z
50 FILES
60 LINEINPUT"Input file? ";WI 
70 LINEINPUT"Output file? (ENTER for LCD:) ";WO:IF WO=""THEN WO="LCD:" 
80 R="": S="" 
90 PRINT"Search for?" 
100 GOSUB 230: S=T 
110 PRINT"Replace with?" 
120 GOSUB 230: R=T 
130 LS=LEN(S) 
140 K=0 
150 OPEN WI FOR INPUT AS 1 
160 OPEN WO FOR OUTPUT AS 2 
170 PS=1: T="" 
180 IF EOF(1)THENPRINT#2,T;: PRINT"Found"K: CLOSE: END 
190 Q=INPUT$(1,1) 
200 IF Q=MID$(S,PS,1)THEN T=T+Q: IF PS=LS THEN220ELSE PS=PS+1: GOTO 180
210 PRINT#2,T;Q;: GOTO 170
220 K=K+1: PRINT#2,R;: GOTO 170
230 T="" 
240 PRINT"(^Z when done, ^C to cancel)" 
250 U=INPUT$(1): IF U=CHR$(26)THEN330
260 IF U<>CHR$(8)THEN300
270 IF LEN(T)=0THEN250
280 PRINT CHR$(127);: IF RIGHT$(T,1)<" "THENPRINTCHR$(127); 
290 T=LEFT$(T,LEN(T)-1): GOTO250
300 T=T+U 
310 IF U>=CHR$(32)THENPRINTU;: GOTO250
320 PRINT "^"CHR$(ASC(U)+64);: GOTO250
330 PRINT: RETURN 
