10 'CALC.BA: Calculator Program
20 'Copyright 1983 by Tandy Corp. All rights reserved.
30 MAXFILES=0: CLEAR 256: DEFINT A-U,X-Z: DEFDBL V
40 T=30: DIM VL(T),V(6)
50 FOR I=1 TO 6:V(I)=0: NEXT I
60 SG$="+-": DG$="0123456789": RN$=DG$+".": VA$="ABCDER": VB$="AaBbCcDdEeRr"
70 OP$="^*/\@+-":PR$="1223344": LP$="(": RP$=")": BF$=STRING$(16,32)
80 WV=40: Z4=WV*3: Z5=WV*4: Z7=WV*6: Z8=WV*7
90 ES$=CHR$(27): EL$=ES$+"K": EF$=ES$+"J": VH$=ES$+"p": VN$=ES$+"q"
100 CLS
110 FOR I=1 TO 6: PRINT@ WV/2*(I-1),MID$(VA$,I,1)"="CSNG(V(I)) EL$;: NEXT I
120 PRINT@Z4,"Current formula:"; EL$: PRINT E1$
130 PRINT@Z8,"Select: <F>ormula <D>ata <C>alc <Q>uit ";
140 A$=INPUT$(1): ON INSTR(1,"FfDdCcQq",A$)/2+.5 GOTO 160,190,250,830
150 GOTO 140
160 PRINT@Z4,"Recognized operators: "OP$; " and ( )";EF$;
170 PRINT@Z8,"Press Enter when done";EF$;: PRINT @Z5,"";
180 LINE INPUT E1$: E2$=E1$: GOTO 120
190 PRINT@Z7,EF$: PRINT "Change which variable ("VA$")? ";
200 A$=INPUT$(1): NV=INSTR(1,VB$,A$)/2+.5: IF NV=0 THEN 130
210 PV=WV/2*(NV-1)+2: PRINT@PV,VH$ CSNG(V(NV)) VN$;
220 NV$=STR$(V(NV)): PRINT @Z7,EF$;: INPUT "Enter a new value"; NV$
230 V(NV)=VAL(NV$)
240 PRINT@PV,BF$;: PRINT @PV,CSNG(V(NV));: GOTO 190
250 PRINT@Z7,"Calculating . . .";EL$:LE=LEN(E1$)
260 LR=INSTR(1,E1$,RP$): IF LR=0 THEN E$=E1$: GOTO 340
270 IF LR<2 THEN 810
280 LL=0
290 FOR SP=LR-1 TO 1 STEP -1
300 C$=MID$(E1$,SP,1): IF C$=LP$ THEN LL=SP: SP=1
310 NEXT SP
320 IF LL=0 THEN 810
330 E$=MID$(E1$,LL+1,LR-LL-1)
340 L=LEN(E$)
350 P=1: EOT=0: TC=0: OC=0: PF=0
360 FOR I=1 TO T: VL(I)=0: NEXT I: O$="": TL$=""
370 TF=0: S$="+": I$="": F$="": GOSUB 840: IF EOT THEN 490
380 IF INSTR(1,SG$,C$)=0 THEN 400
390 S$=S$+C$: GOSUB 840: IF EOT THEN 490ELSE 380
400 IF INSTR(1,RN$,C$)>0 THEN 440
410 IF INSTR(1,VB$,C$)>0 THEN 430
420 GOTO 490
430 I=INSTR(1,VB$,C$)/2+.5: VL=V(I): TF=-1: GOSUB 840: GOTO 500
440 IF INSTR(1,DG$,C$)>0 THEN 460
450 IF C$="." THEN 470ELSE 490
460 I$=I$+C$: TF=-1: GOSUB 840: IF EOT THEN 490ELSE 440
470 GOSUB 840: IF EOT THEN 490
480 IF INSTR(1,DG$,C$)>0 THEN F$=F$+C$: TF=-1: GOTO 470
490 VL=VAL(I$+"."+F$)
500 SN=1: SF=1
510 SF=INSTR(SF,S$,"-"): IF SF>0 THEN SF=SF+1: SN=-SN: GOTO 510
520 VL=VL*SN
530 IF NOT TF THEN 810
540 TC=TC+1: IF TC>T THEN 820ELSE VL(TC)=VL
550 IF EOT THEN 580
560 IF INSTR(1,OP$,C$)=0 THEN 810
570 PF=-1: OC=OC+1: O$=O$+C$: TL$=TL$+MID$(PR$,INSTR(1,OP$,C$),1): GOTO 370
580 FOR TN=1 TO 4
590 SP=1
600 TF=INSTR(SP,TL$,CHR$(TN+48)): IF TF=0 THEN 710
610 BP$=MID$(O$,TF,1): BF=INSTR(1,OP$,BP$)
620 ON BF GOSUB 740,750,760,770,780,790,800
630 IF TF+1=TC THEN 670
640 FOR TM=TF+1 TO TC-1
650 VL(TM)=VL(TM+1)
660 NEXT TM
670 TC=TC-1
680 TL$=LEFT$(TL$,TF-1)+RIGHT$(TL$,OC-TF)
690 O$=LEFT$(O$,TF-1)+RIGHT$(O$,OC-TF)
700 OC=OC-1: SP=TF: IF SP<=OC THEN 600
710 NEXT TN
720 IF LR=0 THEN PRINT @Z7,"Result is: ";VL(1) EL$;:V(6)=VL(1):E1$=E2$: GOTO 110
730 E1$=LEFT$(E1$,LL-1)+STR$(VL(1))+RIGHT$(E1$,LE-LR): GOTO 250
740 VL(TF)=VL(TF)^VL(TF+1): RETURN
750 VL(TF)=VL(TF)*VL(TF+1): RETURN
760 VL(TF)=VL(TF)/VL(TF+1): RETURN
770 VL(TF)=VL(TF)\VL(TF+1): RETURN
780 VL(TF)=VL(TF)MOD VL(TF+1): RETURN
790 VL(TF)=VL(TF)+VL(TF+1): RETURN
800 VL(TF)=VL(TF)-VL(TF+1): RETURN
810 BEEP: PRINT @Z7,"Error: Check formula"EL$;: E1$=E2$: GOTO 110
820 BEEP:PRINT@Z7, "Error: Formula contains >"T"terms."EL$;: E1$=E2$: GOTO 110
830 MENU
840 IF P>L THEN EOT=-1: RETURN
850 C$=MID$(E$,P,1): P=P+1
860 IF C$=" " THEN 840ELSE RETURN
