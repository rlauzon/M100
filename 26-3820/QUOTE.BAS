10 'QUOTE.BA: Automatic Data Retrieval Program
20 'Copyright 1983 by Tandy Corp. All rights reserved.
30 MAXFILES=3:CLEAR 256,HIMEM-14:DEFINT A-Z:DIM QQ
40 AD!=HIMEM:FOR I=1 TO 13:READ B:POKE AD!+I,B:NEXT I:RC!=HIMEM+1
50 READ PH$
60 M!=VARPTR(PH$):CD!=PEEK(M!+1)+PEEK(M!+2)*256
70 RP$(0)="The call did not go through."
80 RP$(1)="Unable to log-on."
90 RP$(2)="The call was completed normally."
100 RP$(3)="Call stopped after reaching time limit."
110 RP$(4)="The call was terminated by "
120 CLS
130 TF=0:INPUT "Call at what time (hh:mm:ss)";T1$:IF T1$=""THEN 200
140 ON ERROR GOTO 180:ON TIME$=T1$ GOSUB 190:TIME$ ON:ON ERROR GOTO 0    
150 INPUT"Time limit for call (1-60 minutes)";TL:IF TL<1 OR TL>60 THEN 150
160 CLS:PRINT@80,"Call-alarm is set for "T1$
170 IF TF=1 THEN 200ELSE PRINT@160,"The time is "TIME$: GOTO 170
180 BEEP:PRINT"Invalid time format":RESUME 130
190 TF=1:TIME$ OFF:RETURN
200 A$=TIME$:AH!=VAL(MID$(A$,1,2))*3600:AM=(TL+VAL(MID$(A$,4,2)))*60:AS=VAL(MID$(A$,7,2)):AT!=AH!+AM+AS
210 AH=INT(AT!/3600):A1=AT!-AH*3600:AM=A1\60:AS=A1-AM*60
220 A=AH MOD 24: GOSUB 270: AH$=A$
230 A=AM:GOSUB 270:AM$=A$
240 A=AS:GOSUB 270:AS$=A$
250 T2$=AH$+":"+AM$+":"+AS$
260 GOTO 280
270 A1$=STR$(A):A$=RIGHT$("0"+RIGHT$(A1$,LEN(A1$)-1),2):RETURN
280 CLS:CC=0:ON ERROR GOTO 610
290 ON TIME$=T2$ GOSUB 620: TIME$ ON
300 T1$=TIME$:PRINT:PRINT"Calling host now.":CALL 21200
310 CALL 21293,0,CD!
320 CC=1:PRINT:PRINT"Host computer is on-line.":PRINT"Logging-on and requesting data..."
330 OPEN "quotes" FOR OUTPUT AS 3
340 PRINT #3,DATE$" "TIME$
350 OPEN "MDM:7I1E" FOR INPUT AS 1: OPEN "MDM:7I1E" FOR OUTPUT AS 2
360 CC=2
370 READ NL: L=1
380 READ CM$,ST$,SC:GOSUB 490:IF CC=3 THEN 400
390 L=L+1:IF L<=NL THEN 380
400 CLOSE: CALL 21179
410 PRINT:PRINT"On at "T1$". Off at "TIME$
420 PRINTRP$(CC)
430 IF CC=4 THEN PRINT "error number"ER"in line"EL
440 CLEAR 0,HIMEM+14:END
450 SP=1
460 P=INSTR(SP,TX$,"^"):IF P=0 THEN RETURN
470 SP=P+1:C$=MID$(TX$,SP,1):IF C$>="@"AND C$<"\" THEN TX$=LEFT$(TX$,P-1)+CHR$(ASC(C$)-64)+RIGHT$(TX$,LEN(TX$)-P-1)
480 GOTO 460
490 TX$=CM$:GOSUB 450:CM$=TX$
500 TX$=ST$:GOSUB 450:ST$=TX$
510 PRINT#2,CM$;
520 P=1
530 SC$=MID$(ST$,P,1)
540 IF CC=3 THEN RETURN ELSE CALL RC!,0,VARPTR(QQ):IF QQ=255 THEN 540
550 CC$=CHR$(QQ):IF SC=1 AND CC=2 THEN PRINT#3,CC$;
560 PRINT CC$;
570 IF SC$<>CC$THEN 520
580 P=P+1:IF P<=LEN(ST$)THEN 530
590 CALL RC!,0,VARPTR(QQ):IF QQ<255 THEN PRINTCHR$(QQ);:GOTO 590
600 RETURN
610 CC=4:ER=ERR:EL=ERL:RESUME 400
620 CC=3:RETURN
630 REM -- machine code subroutine
640 DATA 235,205,109,109,62,255,18,200,205,126,109,18,201
650 REM -- phone number
660 DATA 555-1111<>
670 REM -- number of dialog triplets
680 DATA 14
690 REM -- dialog triplets including log-on/log-off
700 DATA ^C,U,0
710 DATA "70000,999^M",P,0
720 DATA PASSWORD^M,"!",0
730 DATA G FIN-6^M,":",0
740 DATA QUOTES^M,":",0
750 DATA ^M,"^JI",0
760 DATA XON^M,^JI,0
770 DATA T^M,^JI,0
780 DATA BUD^M,^JI,0
790 DATA TAN^M,^JI,0
800 DATA IBM^M,^JI,0
810 DATA ^M,!,0
820 DATA "1,2,3,4,5^M",PROGRAM,1
830 DATA /OFF^M,Off,0
